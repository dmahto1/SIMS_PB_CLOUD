$PBExportHeader$f_calc_gsin_check_digit.srf
$PBExportComments$S29552 F13772 I1745 PhilipsCLS BlueHeart
global type f_calc_gsin_check_digit from function_object
end type

forward prototypes
global function integer f_calc_gsin_check_digit (string asin)
end prototypes

global function integer f_calc_gsin_check_digit (string asin);Integer	liCheck, liPos, liOddSum, liEvenSum, liSum, liLen, liRemainder
String	lsCode

lsCode = asIn
liLen = Len(lsCode)

//Must be 16 digits!  GSIN
If liLen <> 16 Then return -1

//Sum The Odd Digits
liPos = liLen	 /* first odd digit */
Do While liPos > 0
	
	liOddSum += Long(Mid(lsCode,liPos,1))
	liPos -=2 		/*next odd number*/
	
Loop

liOddSum = liOddSum * 3 /* multiply odd sum by 3 */

//Sum the Even Digits
liPos = liLen -1 	/* first Even digit */
Do While liPos > 0
	
	liEvenSum += Long(Mid(lsCode,liPos,1))
	liPos -=2 /*next odd number*/
	
Loop

liSum = liOddsum + liEvenSum /* add the odd and even sums together*/

//Divide Sum by 10 to get remainder
liRemainder = Mod(liSum, 10)

//Check digit is number which add remainder to 10
liCheck = 10 - liRemainder

If liCheck = 10 then liCheck = 0

Return liCheck
end function

